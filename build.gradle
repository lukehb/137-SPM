plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'onethreeseven'
version '0.0.1-SNAPSHOT'
ext.moduleName = 'onethreeseven.spm'


allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'idea'
}

repositories {
    //maven{url 'https://dl.bintray.com/lukehb/137-geo'} //geography module hosted on bintray
    //maven { url 'https://github.com/lukehb/137-jars/raw/master/repository' } //spmf
    mavenCentral()
    mavenLocal()
}

dependencies {

    compile group: 'onethreeseven', name: 'collections', version: '0.0.2'
    compile group: 'onethreeseven', name: 'common', version: '0.0.5-SNAPSHOT'

    //plugin architecture
    compile group: 'onethreeseven', name: 'trajsuitePlugin', version: '0.0.1-SNAPSHOT'

    //jcli
    compile group: 'onethreeseven', name: 'jclimod', version: '0.0.1-SNAPSHOT'

    //spmf

    //geo
    //compile group: 'onethreeseven', name: 'geo', version: '0.0.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Automatic-Module-Name': moduleName,
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'))
    }
}

jar.dependsOn(test)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc{
    options.addStringOption('-module-path', classpath.asPath)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true //If version should be auto published after an upload
    pkg {
        repo = '137-SPM'
        name = '137-SPM'
        licenses = ['MIT']
        desc = 'Sequential Pattern Mining algorithms written in Java - comes with a CLI.'
        websiteUrl = 'https://github.com/lukehb/137-SPM'
        issueTrackerUrl = 'https://github.com/lukehb/137-SPM/issues'
        vcsUrl = 'https://github.com/lukehb/137-SPM.git'
        labels = ['SPM', 'java', 'CLI', 'data-mining', 'sequential pattern mining']
        publicDownloadNumbers = true
        githubRepo = 'lukehb/137-SPM'
        githubReleaseNotesFile = 'README.md'
    }
}